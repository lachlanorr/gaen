//------------------------------------------------------------------------------
// utils.cmp - Misc utilities
//
// Gaen Concurrency Engine - http://gaen.org
// Copyright (c) 2014-2021 Lachlan Orr
//
// This software is provided 'as-is', without any express or implied
// warranty. In no event will the authors be held liable for any damages
// arising from the use of this software.
//
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
//
//   1. The origin of this software must not be misrepresented; you must not
//   claim that you wrote the original software. If you use this software
//   in a product, an acknowledgment in the product documentation would be
//   appreciated but is not required.
//
//   2. Altered source versions must be plainly marked as such, and must not be
//   misrepresented as being the original software.
//
//   3. This notice may not be removed or altered from any source
//   distribution.
//------------------------------------------------------------------------------

// Send a message to self entity at a certain interval
component Timer
{
    float #interval = 1.0;          // interval in seconds to send the timer message
    int #message = #timer;         // message to send with each timer message, useful if multiple timers are used in the same component

    float lastNotification = 0.0;

    update
    {
        if (interval > 0.0)
        {
            lastNotification += delta;
            if (lastNotification >= interval)
            {
                @:message();
                lastNotification = 0.0;
            }
        }
    }
}

component Animation
{
    asset #gaim;
    int #modelUid;
    int animIdx;
    int animFrameCount;
    int #animState
    {
        default = #default;
        post
        {
            animIdx = $.gaim_anim_index(gaim, animState);
            animFrameCount = $.gaim_frame_count(gaim, animIdx);
        }
    }
    bool #looped = false;

    int animOffset = 0;
    float animDelta = 0.0;

    #setAnimState(int state)
    {
        animState = state;
    }

    update
    {
        if (animState != #default)
        {
            animDelta += delta;
            int newOffset = $.gaim_frame_offset(gaim, animIdx, animDelta, looped);
            if (newOffset != animOffset)
            {
                animOffset = newOffset;
                @renderer#model_frame_offset(modelUid, animOffset);
            }
        }
    }
}

//------------------------------------------------------------------------------

component WasdCamera
{
    vec3 dirForwardInit = vec3{ 0.0, 0.0, -1.0 };
    vec3 dirRightInit = vec3{ 1.0, 0.0, 0.0 };
    vec3 dirUpInit = vec3{ 0.0, 1.0, 0.0 };

    vec3 dirForward = dirForwardInit;
    vec3 dirRight = dirRightInit;
    vec3 dirUp = dirUpInit;

    quat orientation = quat{ 0.0, 0.0, 0.0, 1.0 };

    vec3 pos = vec3{ 0.0, 0.0, 10.0 };

    float moveDelta = 5.0;
    float rotDelta = 0.0005;

    float forwardBackward = 0.0;
    float leftRight = 0.0;

    bool mouseLooking = false;
    float mouseDeltaX = 0.0;
    float mouseDeltaY = 0.0;

    int mouseWheelDelta = 0;

    #init()
    {
    /*
        $.watch_input_state(#mouse_look, 0, #mouse_look, 1, #mouse_look, 0);
        $.watch_mouse(#mouse_move, #mouse_wheel);

        $.watch_input_state(#forward, 0, #forward_backward,  1, #forward_backward, 0);
        $.watch_input_state(#back,    0, #forward_backward, -1, #forward_backward, 0);
        $.watch_input_state(#left,    0, #left_right,        1, #left_right,       0);
        $.watch_input_state(#right,   0, #left_right,       -1, #left_right,       0);
    */
    }

    #mouse_look(int status)
    {
        mouseLooking = status != 0;
    }

    #forward_backward(int value)
    {
	    forwardBackward = 1.0 * value;
    }

    #left_right(int value)
    {
	    leftRight = 1.0 * value;
    }

    update
    {
        bool hasMoved = false;

        if (mouseWheelDelta != 0)
        {
            pos += dirForward * (mouseWheelDelta * delta * 0.01);
            mouseWheelDelta = 0;
            hasMoved = true;
        }

        if (mouseLooking)
        {
            bool hasRotated = false;
            if (mouseDeltaX != 0.0)
            {
                float angle = -mouseDeltaX * rotDelta;
                quat orientDelta = quat{angle, dirUp};
                orientation = orientDelta * orientation;
                hasRotated = true;
            }

            if (mouseDeltaY != 0.0)
            {
                float angle = mouseDeltaY * rotDelta;
                quat orientDelta = quat{angle, dirRight};
                orientation = orientDelta * orientation;
                hasRotated = true;
            }

            if (hasRotated)
            {
                orientation = normalize(orientation);

                dirForward = orientation * dirForwardInit;
                dirRight = orientation * dirRightInit;

                // LORRTODO - Add dot accessors to language.
                // Zero out dirRight.y, re-normalize, and calc
                // dirUp with dirForward X dirRight.
                // This should correct "roll" weirdness.
                //dirRight.y = 0.0;

                dirUp = orientation * dirUpInit;

                hasMoved = true;
            }

            mouseDeltaX = 0.0;
            mouseDeltaY = 0.0;
        }

        if (forwardBackward != 0.0)
        {
            pos += dirForward * (moveDelta * forwardBackward * delta);
            hasMoved = true;
        }
        if (leftRight != 0.0)
        {
            pos += dirRight * (moveDelta * leftRight * delta);
            hasMoved = true;
        }
        if (hasMoved)
        {
            // LORRTODO: support uid camera/stage system
            $.camera_move(0, pos, orientation);
        }
    }

    #mouse_move(int xDelta, int yDelta)
    {
        //short xDelta = delta >> 16;
        //short yDelta = delta << 16 >> 16;

        //print(string{"delta: %x, xDelta: %d %x, yDelta: %d %x", delta, xDelta, xDelta, yDelta, yDelta});
        //print(string{"xDelta: %d, yDelta: %d", xDelta, yDelta});
        if (mouseLooking)
        {
            mouseDeltaX += xDelta;
            mouseDeltaY += yDelta;
        }
    }

    #mouse_wheel(int delta)
    {
        mouseWheelDelta += delta;
    }
}

// Rotate a model when wasd keys are pressed
component WasdRot
{
    float yawing = 0.0;
    float yaw = 0.0;

    float pitching = 0.0;
    float pitch = 0.0;

    #init()
    {
        //$.watch_input_state(#mouse_look, 0, #mouse_look);
        //$.watch_mouse(#mouse_move, #mouse_wheel);
/*
        $.watch_input_state(#forward, 0, #set_pitch,  1, #set_pitch, 0);
        $.watch_input_state(#back,    0, #set_pitch, -1, #set_pitch, 0);
        $.watch_input_state(#left,    0, #set_yaw,   -1, #set_yaw,   0);
        $.watch_input_state(#right,   0, #set_yaw,   -1, #set_yaw,   0);
*/
    }

    #set_pitch(int value)
    {
        pitching = 1.0 * value;
    }

    #set_yaw(int value)
    {
        yawing = 1.0 * value;
    }

    update
    {
        if (pitching != 0.0 || yawing != 0.0)
        {
            pitch += pitching * delta * 100.0;
            yaw += yawing * delta * 100.0;
            mat43 trans = mat43{vec3{0.0,0.0,0.0},
                                vec3{radians(pitch), radians(yaw), 0.0}};
            @#transform = trans;
        }
    }

    #forward(bool status)
    {
        if (status)
        {
            pitching = 1.0;
        }
        else
        {
            pitching = 0.0;
        }
    }

    #back(bool status)
    {
        if (status)
        {
            pitching = -1.0;
        }
        else
        {
            pitching = 0.0;
        }
    }

    #left(bool status)
    {
        if (status)
        {
            yawing = 1.0;
        }
        else
        {
            yawing = 0.0;
        }
    }

    #right(bool status)
    {
        if (status)
        {
            yawing = -1.0;
        }
        else
        {
            yawing = 0.0;
        }
    }
}

//------------------------------------------------------------------------------

